! -------------------------------------------------------------------------
!
!  mhchem-strict-simplified.grm
!
!  BNF for the MathJax/mhchem syntax,
!  strict, simplified,
!  revision 1,
!  applies to MathJax/mhchem 3.0.0
!
!  - Just for validity check. Does not parse semantically complete.
!  - To be used to texvc.js rules
!  - "strict" = by far not as forgiving as MathJax/mhchem
!  - "simplified" = all features accessible, but not by al
!                   missing many shortcuts
!
! -------------------------------------------------------------------------
!
!  Copyright (c) 2016 Martin Hensel
!
!  Licensed under the Apache License, Version 2.0 (the "License");
!  you may not use this file except in compliance with the License.
!  You may obtain a copy of the License at
!
!      http://www.apache.org/licenses/LICENSE-2.0
!
!  Unless required by applicable law or agreed to in writing, software
!  distributed under the License is distributed on an "AS IS" BASIS,
!  WITHOUT WARRANTIES OR CONDITIONS OF ANY KIND, either express or implied.
!  See the License for the specific language governing permissions and
!  limitations under the License.
!
! -------------------------------------------------------------------------

!-------------
! Definitions
!-------------

"Start Symbol" = <testcases>

"Auto Whitespace" = false


Space = {Space}
NewLine = {CR}{LF}|{CR}
Digit = {Digit}
Letter = {Letter}
Apostrophe = ''


!---------------------
! Rules for test file
!---------------------

<testcases> ::= <testcase>
              | <testcase> <testcases>

<testcase> ::= <ce> NewLine


!------------------------------
! Validity check rules for \ce
!------------------------------

! Just for validity check. Does not parse semantically complete.
! See remarks at top of file.

<ce> ::= <sentence>
           |

<sentence> ::= <phrase>
             | <sentence> Space <phrase>

<phrase> ::= <word> | <word> <single macro> | <single macro> | '^' | '(^)'

<word> ::= <nonletter>
         | Letter
         | <single macro> <nonletter>
         | <word> <nonletter>
         | <word> Letter
         | <word> <single macro> <nonletter>

<nonletter> ::= Digit
           | <script>
           | '{' <literal text> '}'
           | '{' '}'
           | '$' <LaTeX expression> '$'
           | '+' | '-' | '=' | '#' | '(' | ')' | '[' | ']' | '\{' | '\}' | '.' | ',' | ';' | '/' | '*' | '<' | '>' | Apostrophe | '|' | '@' | '&' | '\\'
           | '\bond' '{' <bond type> '}'
           | <ce macro 1p>
           | <ce macro 2p>

<script> ::= <supersub> <script follow>
           | <supersub> '{' <sentence> '}'
           | <supersub> '$' <LaTeX expression> '$'

<supersub> ::= '_' | '^'

<script follow> ::= Digit | Letter | '+' | '-' | '.' | '*' | Apostrophe

<bond type> ::= '-' | '=' | '#' | '~' | '~-' | '~--' | '~=' | '-~-' | '.' '.' '.' | '.' '.' '.' '.' | '<' '-' | '-' '>'
              | Digit  ! in fact, just '1'|'2'|'3'

<single macro> ::= '\alpha' | '\delta' | '\mu' | '\eta' | '\gamma' ! ToDo - Complete the list
                 | '\pm' | '\approx' | '\ca'

<ce macro 1p> ::= '\ce' '{' <ce> '}'
                | '\mathbf' '{' <ce> '}'

<ce macro 2p> ::= '\frac' '{' <ce> '}' '{' <ce> '}'
                | '\color' '{' <color> '}' '{' <ce> '}'
                | '\overset' '{' <ce> '}' '{' <ce> '}'
                | '\underset' '{' <ce> '}' '{' <ce> '}'
                | '\underbrace' '{' <ce> '}' '_' '{' <ce> '}'

! Please re-use validity check rules for \text{...} here.
! The rules written here are just a small subset of the target design.
<literal text> ::= <literal text letter> | <literal text letter> <literal text>
<literal text letter> ::= Digit | Letter | Space | '{' <literal text> '}' | '.' | '-' | '+' | '(' | ')' | '\color' | '\red' | '\frac' | '\alpha' | '\eta' | '\,' | '!' | '\bullet'

! Please re-use rules validity check rules for LaTeX here.
! The rules written here are just a small subset of the target design.
<LaTeX expression> ::= <LaTeX word> | <LaTeX expression> <LaTeX word>
<LateX word> ::= Digit | Letter | Space | '{' <LaTeX expression> '}' | '+' | '-' | '(' | ')' | '\alpha' | '\left' | '\right' | '\ce' | '/' | '\infty' | '\frac' | '_' | '\pm' | Apostrophe | '\mu' | '\eta' | '\underset' | '\color' | '\red' | '\,' | '\bullet' | '\approx' | '^' | '~' | '\mathrm'

! Please re-use validity check rules for \color's colors here (both, for MathJax core and MathJax extension)
! The rules written here are just a small subset of the target design.
<color> ::= '\red' | 'red'
